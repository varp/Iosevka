$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'
import [Dotless CvDecompose] from "../../../support/gr"

glyph-module

glyph-block Letter-Latin-Lower-T : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared : CreateCommaCaronComposition
	glyph-block-import Letter-Shared-Shapes : CurlyTail FlatHookDepth ConnectedCedilla
	glyph-block-import Letter-Shared-Shapes : DiagonalTailR DiagonalTailStdDepth

	define [xSmallTBarLeftT df] : df.middle - TBalance - Stroke * 0.5 * HVContrast
	define xSmallTBarLeft : xSmallTBarLeftT : DivFrame 1
	define [SmallTHookShapeT sink offset top bot sw] : begin
		local hookx : xSmallTBarLeft + (Width - SB * 2) * 0.8 + Stroke * 0.25 * HVContrast
		local turn : [mix xSmallTBarLeft hookx 0.5] + CorrectionOMidS
		local smb  : Math.max Hook ((turn - xSmallTBarLeft) * 1.2)

		return : sink
			widths.lhs sw
			[if (sink == spiro-outline) corner flat] (offset + xSmallTBarLeft) top [heading Downward]
			curl (offset + xSmallTBarLeft) (bot + smb)
			hookend (bot + O + offset) (tight -- !para.isItalic)
			g4 (hookx - offset) (bot + Hook)
			if (sink == spiro-outline) {[corner (hookx - offset) top]} {}

	define [SmallTShape top bot] : glyph-proc
		local g : include : SmallTHookShapeT dispiro 0 top bot Stroke
		set-base-anchor 'bottomright' g.knots.(g.knots.length - 1).x g.knots.(g.knots.length - 1).y
		set-base-anchor 'below' [mix xSmallTBarLeft g.knots.(g.knots.length - 1).x : StrokeWidthBlend 0.375 0.5] bot
		set-base-anchor 'overlay' (g.knots.0.x + 0.625 * Stroke * HVContrast) (top * 0.42)

		include : HCrossBar.top
			xSmallTBarLeft + HalfStroke - LongJut + TBalance2
			xSmallTBarLeft + HalfStroke + LongJut + TBalance2
			begin XH

	create-glyph 't.standard' : glyph-proc
		include : MarkSet.b
		include : SmallTShape CAP 0

	glyph-block-import Letter-Blackboard : BBS BBD
	create-glyph 'mathbb/t' 0x1D565 : glyph-proc
		include : HBarTop xSmallTBarLeft (xSmallTBarLeft + BBD) CAP BBS
		include : SmallTHookShapeT dispiro 0 CAP 0 BBS
		include : intersection
			VBarLeft (xSmallTBarLeft + BBD) 0 CAP BBS
			SmallTHookShapeT spiro-outline 1 CAP 0 BBS
		include : difference
			HCrossBar.top
				xSmallTBarLeft + (BBD / 2) - LongJut + TBalance2
				xSmallTBarLeft + (BBD / 2) + LongJut + TBalance2
				begin XH
				begin BBS
			Rect CAP 0 xSmallTBarLeft (xSmallTBarLeft + BBD)

	define [TFlatShape df top m ext] : glyph-proc
		local xCrossLeft : mix 0 SB m
		local xCrossRight : mix df.width df.rightSB m
		local xBarLeft : [mix xCrossLeft xCrossRight 0.42] - Stroke * 0.375 * HVContrast
		local hd : FlatHookDepth df
		include : union
			HCrossBar.top xCrossLeft (xCrossRight + ext) XH
			dispiro
				widths.rhs
				flat xCrossRight 0
				curl [Math.min (xBarLeft + hd.x) (xCrossRight - 1)] 0
				archv.superness DesignParameters.tightHookSuperness
				flat xBarLeft hd.y
				curl xBarLeft top [heading Upward]
		set-base-anchor 'topright' xCrossRight  CAP
		set-base-anchor 'overlay' (xBarLeft + 0.625 * Stroke * HVContrast) (top * 0.42)
		set-base-anchor 'below' (HalfStroke * HVContrast + [Math.min (xBarLeft + hd.x / 2) (xCrossRight - 1)]) 0

	define dfNarrowT : DivFrame para.diversityF
	create-glyph 't.flatHook' : glyph-proc
		set-width dfNarrowT.width
		include : dfNarrowT.markSet.b
		include : TFlatShape dfNarrowT CAP [mix 1 para.diversityF 2] 0

	create-glyph 't.flatHookShortNeck' : glyph-proc
		set-width dfNarrowT.width
		include : dfNarrowT.markSet.b
		include : TFlatShape dfNarrowT [Math.min CAP ([mix XH CAP 0.75] + Stroke * 0.25)] [mix 1 para.diversityF 2] 0

	create-glyph 't.flatHookShortNeck2' : glyph-proc
		set-width dfNarrowT.width
		include : dfNarrowT.markSet.b
		include : TFlatShape dfNarrowT [Math.min CAP ([mix XH CAP 0.625] + Stroke * 0.25)] [mix 1 para.diversityF 2] 0

	create-glyph 't.cross' : glyph-proc
		define df dfNarrowT
		set-width df.width
		include : df.markSet.b
		include : VBar df.middle 0 CAP
		include : HCrossBar.top (df.middle - LongJut * df.div) (df.middle + LongJut * df.div) XH
		set-base-anchor 'overlay' df.middle (CAP * 0.42)

	create-glyph 't.hooklessAsymmetric' : glyph-proc
		define df dfNarrowT
		define xBar : xSmallTBarLeftT df
		set-width df.width
		include : df.markSet.b
		include : VBarLeft xBar 0 CAP
		include : HCrossBar.top xBar (df.middle + df.div * LongJut) XH
		set-base-anchor 'overlay' (xBar + 0.625 * Stroke * HVContrast) (CAP * 0.42)
		set-base-anchor 'below' (xBar + HalfStroke * HVContrast) 0

	define [DiagTailedSmallTHookShape df top bot sw] : begin
		local xMid : xSmallTBarLeft + HalfStroke * HVContrast
		return : dispiro
			flat xMid top [widths.center.heading sw Downward]
			DiagonalTailR df xMid bot [DiagonalTailStdDepth df sw] sw

	define [TDiagTailShape df top] : glyph-proc
		local g : include : DiagTailedSmallTHookShape df top 0 Stroke
		set-base-anchor 'overlay' (g.knots.0.x + 0.125 * Stroke * HVContrast) (top * 0.42)

		include : HCrossBar.top
			xSmallTBarLeft + HalfStroke - LongJut + TBalance2
			xSmallTBarLeft + HalfStroke + LongJut + TBalance2
			begin XH

	create-glyph 't.diagonalTailed' : glyph-proc
		include : MarkSet.b
		include : TDiagTailShape [DivFrame 1] CAP


	create-glyph 't.phoneticLeft' : glyph-proc
		local df : DivFrame 1
		set-width df.width
		include : df.markSet.b
		include : TFlatShape df CAP 1 0

	create-glyph 't.phoneticLeft.extended' : glyph-proc
		local df : DivFrame 1
		set-width df.width
		include : df.markSet.b
		include : intersection
			TFlatShape df CAP 1 Width
			Rect CAP 0 SB (Middle + Width)

	select-variant 't' 't'

	turned 'turnt.standard'           nothing 't.standard'           Middle           (XH / 2) [MarkSet.p]
	turned 'turnt.diagonalTailed'     nothing 't.diagonalTailed'     Middle           (XH / 2) [MarkSet.p]
	turned 'turnt.cross'              nothing 't.cross'              dfNarrowT.middle (XH / 2) [dfNarrowT.markSet.p]
	turned 'turnt.flatHook'           nothing 't.flatHook'           dfNarrowT.middle (XH / 2) [dfNarrowT.markSet.p]
	turned 'turnt.hooklessAsymmetric' nothing 't.hooklessAsymmetric' dfNarrowT.middle (XH / 2) [dfNarrowT.markSet.p]
	turned 'turnt.flatHookShortNeck'  nothing 't.flatHookShortNeck'  dfNarrowT.middle (XH / 2) [dfNarrowT.markSet.p]
	turned 'turnt.flatHookShortNeck2' nothing 't.flatHookShortNeck2' dfNarrowT.middle (XH / 2) [dfNarrowT.markSet.p]
	select-variant 'turnt' 0x287 (follow -- 't')

	CreateCommaCaronComposition 't' 'tcaron' 0x165

	with-related-glyphs 'tCedilla' 0x163 't' ConnectedCedilla

	create-glyph 'tltail' 0x1AB : glyph-proc
		include : MarkSet.if
		include : refer-glyph 't.standard'
		local attach : currentGlyph.gizmo.unapply [query-glyph 't.standard'].baseAnchors.bottomright
		include : VBarRight attach.x 0 attach.y
		include : VerticalHook (attach.x - HalfStroke * HVContrast) 0 (-HookX) Hook

	create-glyph 'thooktop' 0x1AD : glyph-proc
		include : MarkSet.e
		include : SmallTShape (XH - HalfStroke) 0
		include : VerticalHook (xSmallTBarLeft + HalfStroke * HVContrast) XH HookX (-Hook)

	create-glyph 'trtail' 0x288 : glyph-proc
		include : MarkSet.if
		include : SmallTShape CAP Descender

	create-glyph 'tcurlytail' 0x236 : glyph-proc
		include : MarkSet.b
		local fine : AdviceStroke 3.5
		local rinner : LongJut / 2 - fine / 2
		local x2 : mix SB xSmallTBarLeft 0.4
		local y2 : -fine
		include : HBarTop (xSmallTBarLeft + HalfStroke - LongJut + TBalance2) (xSmallTBarLeft + HalfStroke + LongJut + TBalance2) XH
		include : dispiro
			widths.lhs
			flat xSmallTBarLeft CAP [heading Downward]
			curl xSmallTBarLeft (fine + rinner * 2)
			CurlyTail fine rinner (xSmallTBarLeft+ Stroke * HVContrast) 0 (xSmallTBarLeft + Stroke * HVContrast + LongJut) x2 y2
