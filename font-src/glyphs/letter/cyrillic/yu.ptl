$$include '../../../meta/macros.ptl'

import [mix barmixL linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'
import '../../../support/point' as Point

glyph-module

glyph-block Letter-Cyrillic-Yu : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	define [CyrYuShape df top xtop sma smb ltJutSide] : glyph-proc
		local xm : barmixL df.leftSB df.rightSB (df.mvs * HVContrast) [StrokeWidthBlend 0.4 0.45]
		include : VBarLeft df.leftSB 0 xtop df.mvs
		include : OShape top 0 xm df.rightSB df.mvs (sma * 0.7 * df.div) (smb * 0.7 * df.div)
		include : HBar (df.leftSB + 1) xm (top / 2)

		if SLAB : begin
			include : tagged "serifLT"
				if ltJutSide
					LeftwardTopSerif df.leftSB xtop (Jut * 0.75 - df.mvs / 2 * HVContrast)
					CenterTopSerif (df.leftSB + df.mvs / 2 * HVContrast) xtop (Jut * 0.75)
			include : tagged "seriflB"
				CenterBottomSerif (df.leftSB + df.mvs / 2 * HVContrast) 0 (Jut * 0.75)

	create-glyph 'cyrl/Yu' 0x42E : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.capital
		include : CyrYuShape df CAP CAP SmoothA SmoothB

	create-glyph 'cyrl/yu' 0x44E : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.e
		include : CyrYuShape df XH XH SmallSmoothA SmallSmoothB

	create-glyph 'cyrl/yu.BGR' : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.b
		include : CyrYuShape df XH CAP SmallSmoothA SmallSmoothB true